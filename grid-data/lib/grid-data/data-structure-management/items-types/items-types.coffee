PACK.items_types = {}

_.extend GridData.prototype,
  _initItemsTypes: ->
    # Merge the built-in items types settings with those provided with the @options
    # (if any) to @items_types_settings
    #
    # XXX note: at the moment we override built-in items types settings properties
    # and not perform deep merge.
    @items_types_settings = {}
    for type, type_settings of PACK.items_types
      custom_settings = @options?.items_types_settings?[type]
      @items_types_settings[type] = _.extend {}, type_settings, custom_settings

    # add to @items_types_settings settings for items defined only in @options
    if (custom_items_types_settings = @options?.items_types_settings)?
      for type, type_settings of custom_items_types_settings
        if not (type of @items_types_settings)
          @items_types_settings[type] = type_settings

    # Register the custom metadata generators
    @custom_items_types_metadata_generators = {}
    for item_type, item_type_custom_options of @items_types_settings
      if (generator = item_type_custom_options.metadataGenerator)?
        @registerItemTypeGenerator(item_type, generator)

    # Register the main metadata in charge on items types
    @registerMetadataGenerator (item, ext, index) =>
      type = @getItemType(index) or "default"

      if (builtInTypeMetadataGenerator = @items_types_settings[type]?.builtInMetadataGenerator)?
        metadata = builtInTypeMetadataGenerator(item, ext, index)
      else
        metadata = {}

      if (custom_item_type_metadata_generator = @custom_items_types_metadata_generators[type])?
        for generator in custom_item_type_metadata_generator
          generator(metadata, item, ext, index) # metadata is passed by refernce, no need to capture returned value.

      # Add a class with the item's type id
      type_class = "type-#{type}"
      if metadata.cssClasses?
        metadata.cssClasses.push type_class
      else
        metadata.cssClasses = [type_class]

      return metadata

    return

  registerItemTypeGenerator: (item_type, metadata_generator) ->
    # registers a custom metadata generator for `item_type` 
    # Unlike regular metadata generators, the provided metadata_generator
    # will be called with the following arguments:
    #
    #   metadata_generator(metadata, item, ext, index)
    #
    # `metadata` arg is an object, passed by reference with the accumulated metadata
    # settings generated by the default metadata generator of this type (only) and
    # previously called custom metadata generators of this item_type.
    # You should edit this object with the changes (additions, changes removals of
    # properties) you want to apply.
    # Since the item is passed by reference, there's no need to return the
    # metadata object (will have no effect).
    if not @custom_items_types_metadata_generators[item_type]?
      @custom_items_types_metadata_generators[item_type] = []

    @custom_items_types_metadata_generators[item_type].push metadata_generator